#library "GLOBAL_ACS_STUFF"

#include "zcommon.acs"

/***
 * Constants
 */
#define TICS_PER_SECOND 35
#define CORPSES_TO_CLEAN 6
#define FAKE_CORPSES_TO_CLEAN_TOTAL 65535

#define FAKE_PLAYER_TID 100
#define BUDDY_TID 333
#define BOSS_TID 666

#define PLAYER_TYPE_IMP 0
#define PLAYER_TYPE_FORMERHUMAN 1

#define MUSIC_E1M1 0
#define MUSIC_OFFICE 1
#define MUSIC_E1M3 2

/***
 * Variables
 */
bool got_cup = false;
bool bosstalk = false;
int corpses_cleaned = 0;

/***
 * Global functions
 */
 
script "SetMusic" (int track)
{
	Log(s:"SetMusic:", i:track);

	switch (track) {
		case MUSIC_E1M1:
			SetMusic("D_E1M1", 0);
			break;
		case MUSIC_OFFICE:
			SetMusic("music/DoomJazz.mp3", 0);
			break;
		case MUSIC_E1M3:
			SetMusic("D_E1M3", 0);
			break;
		default: {
			Log(s:"Unrecognized music track ", i:track);
			SetMusic("", 0);
			break;
		}
	}
}

script "SetPlayerType" (int type)
{
	switch (type) {
		case PLAYER_TYPE_IMP:
			ClearInventory();
			GiveInventory("ImpGun", 1);
			break;
		case PLAYER_TYPE_FORMERHUMAN: {
			ClearInventory();
			GiveInventory("Fist", 1);
			GiveInventory("Pistol", 1);
			GiveInventory("Clip", 50);
			break;
		}
		default:
			Log(s:"Unrecognized PlayerType ", i:type);
			break;
	}
}

/**
 * Special trigger scripts
 */

script 33 ENTER
{

  SetCVar ("numPlayerRespawns", 0);
  
  ACS_NamedExecute("SetMusic", 0, MUSIC_E1M1);
  
  GiveInventory("ImpGun",1);
  
  SetWeapon("ImpGun");
 
}

script 1 (void) //Cross line near coffee guy

{

	if (GetCVar ("numPlayerRespawns") == 1)
	
		{

		PlaySound(BUDDY_TID, "impgibberishcoffee");

		print(s:"\c[Green]This coffee sucks!!!");
		
		}

}



script 2 (void) //Cross first line into to boss office area

{

	if (GetCVar ("numPlayerRespawns") == 1)
		{
			print(s:"\c[Red]What the hell are you doing?");

			PlaySound(BOSS_TID, "bossthatdoesntwork");

			delay(3 * TICS_PER_SECOND);

			print(s:"\c[Red]Get back to work! ... Platform 1 please!");

			PlaySound(BOSS_TID, "bossgetbacktowork");
			
			door_raise(982,128,0,0);
			
		}
}



script 3 (void) //First teleport out to work

{
	if (GetCVar ("numPlayerRespawns") == 1)
	
		{
			Door_Raise(982,128,0,0);
			
			print(s:"\c[Green]Transmission in progress...");;

			PlaySound(111, "impgibberish3");

			delay(70);

			print(s:"\c[Green]Please Wait");

			PlaySound(111, "impgibberish3");

			Delay(100);

			Teleport(14);

			ACS_NamedExecute("SetMusic", 0, MUSIC_E1M3);

			ACS_NamedExecute("SetPlayerType", 0, PLAYER_TYPE_FORMERHUMAN);

			//SetActorFlag(0, "Invulnerable", TRUE); //This doesn't enable god mode HUD unlike in ZScript :(

			SetActorState(0, "FormerHuman", TRUE);
			
			SpawnSpot("MugGun",545,222,90);

			print(s:"\c[Green]Transmission Complete");

			delay(2 * TICS_PER_SECOND);

			PlaySound(BUDDY_TID, "impgibberish1");

			print(s:"\c[Yellow]Terry? Heyy buddy, what's up?");

			Delay(3 * TICS_PER_SECOND);

			print(s:"\c[Yellow]Haven't seen much of you since you got transferred to E1M1.");
			
			Delay(3 * TICS_PER_SECOND);

			print(s:"\c[Yellow]How are the kids?");
			
			Delay(3 * TICS_PER_SECOND);

			print(s:"\c[Yellow]Hang on... sounds like we've got a live one.");
			
			Delay(2 * TICS_PER_SECOND);
			
			SpawnSpot("EnemyPlayerShotgun", 546, FAKE_PLAYER_TID, 180);
			Thing_SetGoal(FAKE_PLAYER_TID, 98, 0, 0);
			Thing_Hate(BUDDY_TID, FAKE_PLAYER_TID, 0);
			
		}

}



script 5 (void) // Player aproaches broken telepad 

{
	if (GetCVar ("numPlayerRespawns") == 2)
	{
		if (bosstalk == false)
		{
			bosstalk = true;
			
			print(s:"\c[Red]That one isn't working right now.");

			PlaySound(BOSS_TID, "bossthatdoesntwork");

			delay(2 * TICS_PER_SECOND);
		
			if (got_cup == true)
			{

				print(s:"\c[Red]Get back to work!");

				PlaySound(BOSS_TID, "bossgetbacktowork");
				
				delay(3 * TICS_PER_SECOND);
				
			}
			
			else
				
			{
				
				print(s:"\c[Red]Get me some coffeeee!");
				
				PlaySound(BOSS_TID, "bosscoffee");
				
				delay(3 * TICS_PER_SECOND);
				
			}	
			
		}
		bosstalk = false;
	}

}

script "coffeehitboss" (void) //Hit boss with coffee
{
	ACS_NamedTerminate("oops",0);
	got_cup = true;
	
	delay(2 * TICS_PER_SECOND);
	
	door_raise(901,110,0,0);

	PlaySound(BOSS_TID, "bossgetbacktowork");
	
}

script "oops" (void) //Throw coffee
{
	SpawnSpot("MugGun",545);
	delay(3 * TICS_PER_SECOND);
	PlaySound(BOSS_TID, "bosscoffee");
}

script 8 (void) //Demotion Accepted

{
	//ACS_NamedExecute("SetMusic", 0, 2); // On second thought, leaving the office music is kinda funny here

	print(s:"\c[Green]Demotion Accepted");

	delay(55);

	print(s:"\c[Green]Commence Janitorial Duties");

}

script 9 (void) //Janitor Teleport

{

	Teleport(17);

}

script 10 (void) //When you bump his desk
{
	if (GetCVar ("numPlayerRespawns") == 2)
	{
		if (bosstalk == false)
		{
			bosstalk = true;
			if (got_cup == true)
			{
			print(s:"\c[Red]Get back to work!");
			PlaySound(BOSS_TID, "bossgetbacktowork");
			delay(3 * TICS_PER_SECOND);
			}
			else
			{
			print(s:"\c[Red]GET ME SOME COFFEEEE!");
			PlaySound(BOSS_TID, "bosscoffee");
			delay(3 * TICS_PER_SECOND);
			}
			bosstalk = false;
		}
	}
}

script 11 (void) //Janitorial counter
{
	int fake_corpses_to_clean = FAKE_CORPSES_TO_CLEAN_TOTAL - corpses_cleaned;
	if (corpses_cleaned >= CORPSES_TO_CLEAN)
	{
		Playsound(0,"slop");
		print(s:"\c[Green]Things to clean up: ",d:fake_corpses_to_clean);
		fadeto(0, 0, 0, 1.0, 3.0);
		Delay(4 * TICS_PER_SECOND);
		Teleport_EndGame();
	}
	else
	{
		Playsound(0,"slop");
		print(s:"\c[Green]Things to clean up: ",d:fake_corpses_to_clean);
		corpses_cleaned++;
	}
}

script 12 (void) //Second door opens on last office visit
{
	if (GetCVar ("numPlayerRespawns") != 1)
	{
	Door_Raise(983,128,0,0);
	Door_Close(982,128,0);
	}
}
