#library "GLOBAL_ACS_STUFF"

#include "zcommon.acs"

/***
 * Constants
 */
#define PLAYER_RESPAWNS_INITIAL 0 // Set this to 1 and add player 1 start in office to test teleporter
 
#define TICS_PER_SECOND 35
#define CORPSES_TO_CLEAN 6
#define FAKE_CORPSES_TO_CLEAN_TOTAL 65535

#define FAKE_PLAYER_TID 100
#define BUDDY_TID 333
#define BOSS_TID 666

#define PLAYER_TYPE_IMP 0
#define PLAYER_TYPE_FORMERHUMAN 1

#define MUSIC_E1M1 0
#define MUSIC_OFFICE 1
#define MUSIC_E1M3 2

#define TELEPORT1_EDGE 52
#define	DOOR1 982
#define	DOOR2 983
#define	DOOR3 984

#define MEME_TELEPORTER 1001

/***
 * Variables
 */
bool talked_to_buddy = false;
bool got_cup = false;
bool boss_wantsit = true;
bool bosstalk = false;
bool firstdooropened = false;
int corpses_cleaned = 0;

/***
 * Global functions
 */
 
script "SetMusic" (int track)
{
	Log(s:"SetMusic:", i:track);

	switch (track) {
		case MUSIC_E1M1:
			SetMusic("D_E1M1", 0);
			break;
		case MUSIC_OFFICE:
			SetMusic("music/DoomJazz.mp3", 0);
			break;
		case MUSIC_E1M3:
			SetMusic("D_E1M3", 0);
			break;
		default: {
			Log(s:"Unrecognized music track ", i:track);
			SetMusic("", 0);
			break;
		}
	}
}

script "SetPlayerType" (int type)
{
	switch (type) {
		case PLAYER_TYPE_IMP:
			ClearInventory();
			GiveInventory("Fist", 1);
			GiveInventory("ImpGun", 1);
			SetWeapon("ImpGun");
			break;
		case PLAYER_TYPE_FORMERHUMAN: {
			ClearInventory();
			GiveInventory("Fist", 1);
			GiveInventory("Pistol", 1);
			GiveInventory("Clip", 50);
			break;
		}
		default:
			Log(s:"Unrecognized PlayerType ", i:type);
			break;
	}
}

script "BuddyRaise" (void)
{
	PrintBold(s:"\c[Yellow]Very funny, Terry. Quit screwing around or we might get busted again.");
	Delay(3 * TICS_PER_SECOND);
	// HudMessage(s:"\c[Yellow]Have you been out on that ledge so long you forgot how to talk to people?\nPress the ", k:"+use", s:" key.";
		// HUDMSG_PLAIN, 1, CR_YELLOW, 1.5, 0.5, 3); // Nnot working...
	if (!talked_to_buddy) {
		PrintBold(s:"\c[Yellow]Have you been out on that ledge so long you forgot how to talk to people?");
		Delay(3 * TICS_PER_SECOND);
		PrintBold(s:"\c[Yellow]Press the ", k:"+use", s:" key, fool.");
	}
}

script "E1M3PlayerEnter" (void) {
	Print(s:"\c[Yellow]Hang on... sounds like we've got a live one.");
	
	Delay(2 * TICS_PER_SECOND);
	
	SpawnSpot("EnemyPlayerShotgun", 546, FAKE_PLAYER_TID, 180);
	Thing_Hate(BUDDY_TID, FAKE_PLAYER_TID, 6);
	Thing_Hate(FAKE_PLAYER_TID, BUDDY_TID, 4);
	Thing_SetGoal(FAKE_PLAYER_TID, 98, 0, true);		
}

/**
 * Special trigger scripts
 */

script 33 ENTER
{
  SetCVar ("numPlayerRespawns", PLAYER_RESPAWNS_INITIAL);
  
  ACS_NamedExecute("SetMusic", 0, MUSIC_E1M1);
  
  ACS_NamedExecute("SetPlayerType", 0, PLAYER_TYPE_IMP); 
}

script 1 (void) //Cross line near coffee guy
{
	if (GetCVar ("numPlayerRespawns") == 1) {
		PlaySound(BUDDY_TID, "impgibberishcoffee");

		print(s:"\c[Green]This coffee sucks!!!");	
	}

}



script 2 (void) //Cross first line into to boss office area
{
	SetFont("BIGFONT");

	if (GetCVar ("numPlayerRespawns") == 1)
	{
		print(s:"\c[Red]What the hell are you doing?");

		PlaySound(BOSS_TID, "bossthatdoesntworkrev");

		delay(3 * TICS_PER_SECOND);

		print(s:"\c[Red]Get back to work! ... Platform 1 please!");

		PlaySound(BOSS_TID, "bossgetbacktowork");
		
		if	(firstdooropened == false)
		{
			firstdooropened = true;
			Ceiling_RaiseByValue(DOOR1,32,64);
			Door_Close(DOOR3,128,0);
		}	
		
	}
}



script 3 (void) //First teleport out to work
{
	if (GetCVar ("numPlayerRespawns") == 1)
	{
		if	(firstdooropened == false)
		{
			firstdooropened = true;
			Ceiling_RaiseByValue(DOOR1,32,64);
			Door_Close(DOOR3,128,0);
		}
		
		print(s:"\c[Green]Transmission in progress...");;

		//PlaySound(111, "impgibberish3");
		PlaySound(111, "TeleportWarmup");

		delay(2 * TICS_PER_SECOND);

		print(s:"\c[Green]Downloading profile for Former Human Soldier...");

		ACS_NamedExecute("SetPlayerType", 0, PLAYER_TYPE_FORMERHUMAN);
		//PlaySound(111, "impgibberish3");

		Delay(2 * TICS_PER_SECOND);

		print(s:"\c[Green]Stand by for transmission to E1M3...");

		Delay(2 * TICS_PER_SECOND);

		Teleport(14);
		
		ChangeSky("SKY1","SKY1");

		ACS_NamedExecute("SetMusic", 0, MUSIC_E1M3);

		//SetActorFlag(0, "Invulnerable", TRUE); //This doesn't enable god mode HUD unlike in ZScript :(

		SetActorState(0, "FormerHuman", TRUE);
		
		SpawnSpot("MugGun",545,222,90);
		
		Thing_SetSpecial (222, 80, 14,0,0); //Make mug special script trigger
		
		SetLineSpecial (42,80,12);

		print(s:"\c[Green]Transmission Complete");

		delay(2 * TICS_PER_SECOND);

		PlaySound(BUDDY_TID, "impgibberish1");

		print(s:"\c[Yellow]Terry? Heyy buddy, what's up?");

		Delay(3 * TICS_PER_SECOND);

		print(s:"\c[Yellow]Haven't seen much of you since you got transferred to E1M1.");
		
		Delay(3 * TICS_PER_SECOND);
		
		PrintBold(s:"\c[Yellow]Have you been out on that ledge so long you forgot how to talk to people?");
		Delay(3 * TICS_PER_SECOND);
		PrintBold(s:"\c[Yellow]Press the ", k:"+use", s:" key, silly.");
	}
}



script 5 (void) // Player aproaches broken telepad 
{
	SetFont("BIGFONT");
	if (GetCVar ("numPlayerRespawns") == 2)
	{
		if (bosstalk == false)
		{
			bosstalk = true;
			
			print(s:"\c[Red]That one isn't working right now.");

			PlaySound(BOSS_TID, "bossthatdoesntwork");

			delay(2 * TICS_PER_SECOND);
		
			if (boss_wantsit == false)
			{

				print(s:"\c[Red]Get back to work!");

				PlaySound(BOSS_TID, "bossgetbacktowork");
				
				delay(3 * TICS_PER_SECOND);
				
			}
			
			else
				
			{
				
				print(s:"\c[Red]Get me some coffeeee!");
				
				PlaySound(BOSS_TID, "bosscoffee");
				
				delay(3 * TICS_PER_SECOND);
				
			}	
			
		}
		bosstalk = false;
	}

}

script "coffeehitboss" (void) //Hit boss with coffee
{
	if (boss_wantsit == true)
	{
		ACS_NamedTerminate("oops",0);
		
		boss_wantsit = false;
		
		delay(2 * TICS_PER_SECOND);
		
		door_raise(901,110,0,0);

		PlaySound(BOSS_TID, "bossgetbacktowork");
	}
	
}

script "oops" (void) //Throw coffee
{
	SpawnSpot("MugGun",545);
	if(boss_wantsit == true)
	{
		delay(1 * TICS_PER_SECOND);
		PlaySound(BOSS_TID, "bosscoffee");
	}
	else
	{
		delay(1 * TICS_PER_SECOND);
		PlaySound(BOSS_TID, "bossgetbacktowork");
	}
	got_cup = false;
}

script 8 (void) //Demotion Accepted
{
	//ACS_NamedExecute("SetMusic", 0, 2); // On second thought, leaving the office music is kinda funny here

	print(s:"\c[Green]Demotion Accepted");

	delay(55);

	print(s:"\c[Green]Commence Janitorial Duties");

}

script 9 (void) //Janitor Teleport
{

	Teleport(17);

}

script 10 (void) //When you bump his desk
{
	SetFont("BIGFONT");
	if (GetCVar ("numPlayerRespawns") == 2)
	{
		if (bosstalk == false)
		{
			bosstalk = true;
			if (boss_wantsit == false)
			{
			print(s:"\c[Red]Get back to work!");
			PlaySound(BOSS_TID, "bossgetbacktowork");
			delay(3 * TICS_PER_SECOND);
			}
			else
			{
			print(s:"\c[Red]GET ME SOME COFFEEEE!");
			PlaySound(BOSS_TID, "bosscoffee");
			delay(3 * TICS_PER_SECOND);
			}
			bosstalk = false;
		}
	}
}

script 11 (void) //Corpse clean counter
{
	int fake_corpses_to_clean = FAKE_CORPSES_TO_CLEAN_TOTAL - corpses_cleaned;
	if (corpses_cleaned >= CORPSES_TO_CLEAN)
	{
		Playsound(0,"slop");
		print(s:"\c[Green]Things to clean up: ",d:fake_corpses_to_clean);
		fadeto(0, 0, 0, 1.0, 3.0);
		Delay(4 * TICS_PER_SECOND);
		Teleport_EndGame();
	}
	else
	{
		Playsound(0,"slop");
		print(s:"\c[Green]Things to clean up: ",d:fake_corpses_to_clean);
		corpses_cleaned++;
	}
}

script 12 (void) //Second door opens on last office visit
{
	if (GetCVar ("numPlayerRespawns") != 1)
	{
	Ceiling_RaiseByValue(DOOR2,32,64);
	Door_Close(DOOR1,128,0);
	}
}

script 13 (void) // Exit dialogue with buddy
{
	Log(s:"Exit dialoge script called");
	talked_to_buddy = true;
	ACS_Terminate(3, 0);
	Delay(5 * TICS_PER_SECOND);
	ACS_NamedExecute("E1M3PlayerEnter", 0);
}

script 14 (void) //Cup pickup
{
		got_cup = true;
}

script 15 (void) //Debris clean counter
{
	int fake_corpses_to_clean = FAKE_CORPSES_TO_CLEAN_TOTAL - corpses_cleaned;
	if (corpses_cleaned >= CORPSES_TO_CLEAN)
	{
		Playsound(0,"item");
		print(s:"\c[Green]Things to clean up: ",d:fake_corpses_to_clean);
		fadeto(0, 0, 0, 1.0, 3.0);
		Delay(4 * TICS_PER_SECOND);
		Teleport_EndGame();
	}
	else
	{
		Playsound(0,"item");
		print(s:"\c[Green]Things to clean up: ",d:fake_corpses_to_clean);
		corpses_cleaned++;
	}
}

script 16 (void) //Sky Change to Sky3
{
	ChangeSky("SKY3","SKY3");
}

script 17 (void) //Meme teleporter
{
	Teleport(MEME_TELEPORTER, 0, false);
	SetActorState(0, "MemeTeleported", true);
}